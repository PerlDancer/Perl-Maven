#!/usr/bin/perl
use strict;
use warnings;
use 5.010;

use File::Basename qw(basename);
use POSIX;
use JSON::XS qw(encode_json decode_json);
use Time::HiRes ();

my $data_file = '/tmp/perl_maven_logs.json';
my $size_file = '/tmp/perl_maven_logs.size';

my $start_time = Time::HiRes::time;

if (@ARGV) {
	if ( $ARGV[0] eq 'collect' ) {
		collect();

	}
	if ( $ARGV[0] eq 'config' ) {
		my $data = read_file($data_file);

		if ( basename($0) eq 'perl_maven_logs_processing' ) {
			print <<"END_CONFIG";
graph_title Log file processing
graph_vlabel cnt
graph_category PerlMaven
END_CONFIG
			say 'processing_time.label Processing time';
		}

		if ( basename($0) eq 'perl_maven_logs_hostnames' ) {
			print <<"END_CONFIG";
graph_title Page view per domain
graph_vlabel cnt
graph_category PerlMaven
END_CONFIG

			foreach my $host ( keys %{ $data->{hosts} } ) {
				my $name = $host;
				$name =~ s/\./_/g;
				say "$name.label $host";
			}
		}
	}
}
else {
	my $data = read_file($data_file);

	if ( basename($0) eq 'perl_maven_logs_processing' ) {
		say "processing_time.value $data->{processing_time}";
	}

	if ( basename($0) eq 'perl_maven_logs_hostnames' ) {
		foreach my $host ( keys %{ $data->{hosts} } ) {
			my $name = $host;
			$name =~ s/\./_/g;
			say "$name.value $data->{hosts}{$host}";
		}
	}
}

sub read_file {
	my ($file) = @_;

	my $json = '{}';
	if ( open my $fh, '<', $data_file ) {
		local $/ = undef;
		$json = <$fh>;
		close $fh;
	}
	return decode_json $json;
}

sub save_file {
	my ( $file, $text ) = @_;
	open my $out, '>', $file or die "Cannot open '$file' for writing";
	print $out $text;
	close $out;
	return;
}

sub process {
	my ( $file, $location, $data ) = @_;

	#print "Processing $file\n";
	my $cnt = 0;

	my $size = -s $file;
	open my $fh, '<', $file or die "Could not open '$file' for reading";
	seek $fh, $location, 0;
	while ( my $row = <$fh> ) {
		$cnt++;
		chomp $row;
		eval {
			my $entry = decode_json $row;
			if ( defined $entry->{host} ) {
				$data->{hosts}{ $entry->{host} }++;
			}
			else {
				warn "host is missing in line '$row'";
			}
			if ( defined $entry->{elapsed_time} ) {
				$data->{total}{time} += $entry->{elapsed_time};
				$data->{total}{cnt}++;
			}
			1;
		} or do {
			my $err = $@ // 'Unknown error';
			warn "$err  while trying to parse ($cnt) '$row'";
		};
	}

	return $size;
}

sub collect {
	my $time      = time;
	my $this_file = 'logs/' . POSIX::strftime( '%Y-%m-%d-requests.log', gmtime($time) );
	my $prev_file = 'logs/' . POSIX::strftime( '%Y-%m-%d-requests.log', gmtime( $time - 24 * 60 * 60 ) );

	#say $this_file;
	#say $prev_file;

	return if not -e $this_file;

	if ( not -e $size_file ) {
		save_file( $size_file, -s $this_file );
		return;
	}

	open my $fh, '<', $size_file or die "Cannot open '$size_file' for reading";
	my $last_size = <$fh>;
	close $fh;

	my %data;
	if ( -s $this_file < $last_size ) {    # TODO add a condition on time as well!
		if ( -e $prev_file ) {
			process( $prev_file, $last_size, \%data );
		}
	}
	$last_size = process( $this_file, $last_size, \%data );

	$data{processing_time} = Time::HiRes::time - $start_time;

	save_file( $data_file, encode_json( \%data ) );
	save_file( $size_file, $last_size );
}

